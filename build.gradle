plugins {
    id 'java-library'
    id 'maven-publish'
    id "com.github.ben-manes.versions" version "0.39.0"
}

description = 'Spring Boot Platform'

defaultTasks 'clean', 'build'

repositories {
    mavenLocal()
    mavenCentral()
}

allprojects {

    group = 'ch.frostnova'
    version = '1.0.0-SNAPSHOT'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
}

subprojects {

    apply plugin: 'java-library'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withJavadocJar()
        withSourcesJar()
    }
    test {
        useJUnitPlatform()
    }

    artifacts {
        archives sourcesJar
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
        repositories {
            mavenLocal()
        }
    }
}

ext {
    springBootVersion = '2.5.6'
    springTransactionVersion = '5.3.13'

    swaggerVersion = '3.0.0'
    jacksonVersion = '2.13.0'
    jerseyVersion = '3.0.3'

    junitVersion = '5.8.1'
    assertjVersion = '3.21.0'

    jjwtVersion = '0.9.1'
    javaCacheVersion = '1.1.1'
    ehcacheVersion = '3.9.6'

    libs = [
            testbase      : [
                    "org.junit.jupiter:junit-jupiter-api:$junitVersion",
                    "org.junit.jupiter:junit-jupiter-engine:$junitVersion",
                    "org.assertj:assertj-core:$assertjVersion"
            ],
            jackson       : [
                    "com.fasterxml.jackson.core:jackson-annotations:$jacksonVersion",
                    "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion",
                    "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
            ],
            swagger       : [
                    "io.springfox:springfox-swagger2:$swaggerVersion"
            ],
            jwt           : [
                    "io.jsonwebtoken:jjwt:$jjwtVersion"
            ],
            cache         : [
                    "javax.cache:cache-api:$javaCacheVersion",
            ],
            springBoot    : [
                    "org.springframework.boot:spring-boot-starter-web:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-validation:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-aop:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-cache:$springBootVersion",
                    "org.springframework.boot:spring-boot-starter-security:$springBootVersion",
                    "org.springframework:spring-tx:${springTransactionVersion}",
                    "io.springfox:springfox-boot-starter:$swaggerVersion",
                    "io.springfox:springfox-swagger-ui:$swaggerVersion"

            ],
            springBootTest: [
                    "org.springframework.boot:spring-boot-starter-test:$springBootVersion",
            ]
    ]
}

def isNonStable = { version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}